plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
    id 'dagger.hilt.android.plugin'
}

Properties p = new Properties()
p.load(new FileInputStream(rootProject.file('./release.properties')))

android {
    compileSdk 32

    signingConfigs {
        release {
            storeFile file(p.getProperty("KEYSTORE_PATH", "."))
            storePassword p.getProperty("STORE_PASSWORD", ".")
            keyAlias p.getProperty("ALIAS", ".")
            keyPassword p.getProperty("KEY_PASSWORD", ".")
        }

        debug {
            storeFile file("../debug-keystore.jks")
            storePassword "android"
            keyAlias "debug"
            keyPassword "android"
        }
    }

    defaultConfig {
        applicationId "com.ovolk.dictionary"
        minSdk 24
        targetSdk 32
        versionCode p["VERSION_CODE"]?.toInteger() ?: 1
        versionName "1.0.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
    }

    buildTypes {
        // locally
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            applicationIdSuffix = ".debug"
        }

        // release
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        // for demo
        qarelease {
            initWith release
            signingConfig signingConfigs.debug
            applicationIdSuffix ".qa"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.0'
    }
    buildFeatures {
        compose true
        dataBinding true
    }

    // Allow references to generated code
    kapt {
        correctErrorTypes = true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.1'

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt "androidx.lifecycle:lifecycle-compiler:2.5.1"

    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    def work_version = "2.7.1"
    implementation("androidx.work:work-runtime-ktx:$work_version")

    def nav_version = "2.5.1"
    implementation("androidx.navigation:navigation-fragment-ktx:$nav_version")
    implementation("androidx.navigation:navigation-ui-ktx:$nav_version")

    // other
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'androidx.core:core-splashscreen:1.0.0'

    def lottieVersion = "5.0.3"
    implementation "com.airbnb.android:lottie:$lottieVersion"

    // firebase
    implementation platform('com.google.firebase:firebase-bom:30.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // JetPack
    // Integration with activities
    implementation 'androidx.activity:activity-compose:1.5.1'
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.2.1'
    // Animations
    implementation 'androidx.compose.animation:animation:1.2.1'
    // Tooling support (Previews, etc.)
    implementation 'androidx.compose.ui:ui-tooling:1.2.1'
    // Integration with ViewModels
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation "com.google.accompanist:accompanist-appcompat-theme:0.25.1"
    // Constraints
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"

    implementation("com.google.dagger:hilt-android:2.38.1")
    kapt("com.google.dagger:hilt-android-compiler:2.38.1")
    // for hiltViewModel
    implementation("androidx.hilt:hilt-navigation-compose:1.0.0")
}